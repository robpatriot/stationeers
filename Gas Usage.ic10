define minCanPressure 18500
define minTankPressure 2000
define suitsHead 23
define storesHead 17
alias gasDial d0
alias gasCons d1
alias gasBut d2
alias canStore d3
alias gasPressure r0
alias tank r1
alias pump r2
alias ana r3
alias activePump r4
alias dialVal r5
alias currGas r6
alias tmp r14
alias tmp2 r15

start:
yield
l dialVal gasDial Setting
get tmp db dialVal
s db Setting tmp
move sp suitsHead
suitsLoop:
beq sp storesHead gasStores # finished gas types
pop ana
pop pump
pop tank
jal fillCan
j suitsLoop
gasStores:
pop ana # get feed ana
ld gasPressure ana Pressure
bnez gasPressure checkGas # gas -> checkGas
sd activePump On 0 # no gas -> turn off pump
ls tmp canStore 0 Occupied # can?
l tmp2 gasBut Open # button?
and tmp tmp tmp2 # can? and button?
bnez tmp pumpFill # no pres and can and button -> fill
j start
checkGas:
move currGas -1
ld tmp ana RatioOxygen
brlt tmp 1 2
move currGas 0
ld tmp2 ana RatioVolatiles
brlt tmp2 1 2
move currGas 1
sgt tmp tmp 0.3
sgt tmp2 tmp2 0.6
and tmp tmp tmp2
breqz tmp 2
move currGas 2
ld tmp ana RatioNitrogen
brlt tmp 1 2
move currGas 3
ld tmp ana RatioCarbonDioxide
brlt tmp 1 2
move currGas 4
bne currGas -1 pure # have pure gas
pop activePump # Waste pump
j clear
pure:
pop pump
sd pump On 0 # turn off waste pump
ls tmp canStore 0 Occupied # can in store?
beqz tmp pumpClear # pure, no can -> clear
l tmp gasBut Open
beqz tmp start # can, no butt -> nothing
bneq currGas dialVal pumpClear
pumpFill:
add tmp dialVal 10
get tank db tmp
add tmp dialVal 5
get activePump db tmp
sd activePump Mode 1
move pump activePump
jal fillCan
s gasBut Open tmp # reset button if fill finished
j start
fillCan:
ld gasPressure tank Pressure
sgt tmp gasPressure minTankPressure # above min tank pres
beqz tmp setPump # branch so no / 0
div tmp 73500 gasPressure # 30 mols * 295 C * 8 / pressure
sd pump Setting tmp
ld gasPressure ana Pressure # pressure check canister pressure
slt tmp gasPressure minCanPressure # < min pres?
setPump:
sd pump On tmp # switch pump
j ra
pumpClear:
add tmp currGas 5
get activePump db tmp # get pump for selected gas
clear:
sd activePump Mode 0 # pump out
sd activePump Setting 100
sd activePump On 1
j start
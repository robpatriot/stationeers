define minCPress 8500 # normal can
define minSCPress 18500 # smart can
define minTPress 2000 # tank
define suitsHead 23
define storesHead 17
alias gasDial d0
alias gasCons d1
alias gasBut d2
alias canStore d3
alias gasPress r0
alias tank r1
alias pump r2
alias ana r3
alias currPump r4
alias dialVal r5
alias currGas r6
alias tmp r14
alias tmp2 r15

start:
yield
l dialVal gasDial Setting
get tmp db dialVal
s db Setting tmp
move sp suitsHead
suitsLoop:
beq sp storesHead gasStores # finished gas types
pop ana
pop pump
pop tank
move tmp HASH("ItemGasCanisterSmart")
jal fillCan
j suitsLoop
gasStores:
pop ana # get feed ana
ld gasPress ana Pressure
bnez gasPress checkGas # gas -> checkGas
sd currPump On 0 # no gas -> turn off pump
ls tmp canStore 0 Occupied # can?
l tmp2 gasBut Open # button?
and tmp tmp tmp2 # can? and button?
bnez tmp pumpFill # no pres and can and button -> fill
j start
checkGas:
move currGas -1
ld tmp ana RatioOxygen
brlt tmp 1 2
move currGas 0
ld tmp2 ana RatioVolatiles
brlt tmp2 1 2
move currGas 1
sgt tmp tmp 0.3
sgt tmp2 tmp2 0.6
and tmp tmp tmp2
breqz tmp 2
move currGas 2
ld tmp ana RatioNitrogen
brlt tmp 1 2
move currGas 3
ld tmp ana RatioCarbonDioxide
brlt tmp 1 2
move currGas 4
pop currPump # Waste pump
ld tmp currPump On # can get 1 tick pure then mixed
seq tmp2 currGas -1 # -> latch to on if push to waste
or tmp tmp tmp2
sd currPump On tmp
bnez tmp start # push to waste -> start
ls tmp canStore 0 Occupied # can?
beqz tmp butPumpClear # pure, no can -> clear
l tmp gasBut Open
beqz tmp start # can, no butt -> nothing
bne currGas dialVal pumpClear
pumpFill:
add tmp dialVal 10
get tank db tmp
add tmp dialVal 5
get currPump db tmp
move pump currPump
ls tmp canStore 0 PrefabHash
jal fillCan
s gasBut Open tmp # reset button if fill finished
j start
fillCan:
seq tmp tmp HASH("ItemGasCanisterSmart")
select tmp tmp minSCPress minCPress
ld gasPress ana Pressure
sub tmp tmp gasPress # missing press
ld tmp2 tank Pressure
div tmp tmp tmp2 # target press ratio
mul tmp tmp 204 # press ratio / target vol
#ld tmp2 tank Temperature
#mul tmp tmp tmp2
#ld tmp2 ana Temperature
#div tmp tmp tmp2
sd pump Mode 1
sd pump Setting tmp
sgt tmp tmp 0 # press reqd?
sd pump On tmp # switch pump (tmp used on return)
j ra
butPumpClear:
s gasBut Open 0
pumpClear:
add tmp currGas 5
get currPump db tmp # get pump for selected gas
clear:
sd currPump Mode 0 # pump out
sd currPump Setting 100
sd currPump On 1
j start